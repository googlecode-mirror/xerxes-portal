#summary Recipes for Xerxes customizations

<wiki:toc max_depth="1" />







=Dual Subject Pages=

Sets up Xerxes so that the metasearch component is not as prominent.  When a user clicks on a category from the home page, the initial subject page that he or she sees is just a listing of databases, with a link to a separate page where they can search.

Examples:
 
 * [http://library.csun.edu/xerxes/ Cal State Northridge]
 * [http://xerxes.calstate.edu/pomona/ Cal Poly Pomona]
 * [http://xerxes.calstate.edu/sonoma/ Sonoma State University]


== For version 1.5==

  # Copy databases_subject.xsl from your lib/xsl directory into `demo/xsl`.
  # Rename that local copy to `databases_metasearch.xsl`.
  # [http://xerxes-portal.googlecode.com/svn/cookbook/dual-subject-pages/databases_subject.xsl Download this file] into your `demo/xsl`directory. That file is a bare-bones example of a non-metasearch subject page.  You'll certainly want to edit it and add some CSS to make it look better.

== For version 1.6 or higher==

  # [http://xerxes-portal.googlecode.com/svn/cookbook/dual-subject-pages/databases_subject_nosearch.xsl Download this file] into your `demo/xsl` directory.
  # Set-up you local [http://xerxes-portal.googlecode.com/svn/cookbook/dual-subject-pages/actions.xml actions.xml like this].

== Upgrading from 1.5 method to 1.6 method==

  # Delete your local copy of `databases_metasearch.xsl`.
  # Rename `databases_subject.xsl` to `databases_subject_nosearch.xsl`.
  # Set-up your local `actions.xml` as above.

=External Search Box=
Xerxes comes with a feature for [Embed dynamically embedding the search box], and optionally the databases, in external pages.  But if you need more control, here's the HTML and search syntax:

{{{
<form method="get" action="http://xerxes.example.com/demo">
  <input type="hidden" name="base" value="metasearch" />
  <input type="hidden" name="action" value="search" />

  <input type="hidden" name="context" value="Your Name for the Search" />
        
  <input type="hidden" name="context_url" value="http://library.example.com/" />

  <input name="query" type="text" />
  <input type="submit" name="Submit" value="GO" />

  <input name="database" value="CAL01039" type="hidden" />
</form>
}}}

`context` is the name you are giving this grouping of databases -- typically a subject or name like 'Quick Search'.  This will appear in the Xerxes search results pages themselves, in the page header and the breadcrumbs.

`context_url` is the URL of the (external) page you are embedded this search box.  Together with the name given in `context`, Xerxes will use this to construct breadcrumbs back to this page.  For added security, you may also limit this to [Configuration#limit_context_url allowed domains] in config.xml.

You can include as many `database` inputs as you like, up to the search limit set in Metalib.  

Alternately, you can provide the ID of a category in an input called `subject`, and Xerxes will search all the databases in the first subcategory of that subject.  The category ID is the normalized form of the subject name, found at the end of the URL of the subject page.  In [http://xerxes.calstate.edu/demo/databases/subject/environmental-sciences this example] it's 'environmental-sciences'.

Here is the resulting URL:

{{{
http://xerxes.example.com/demo?
  base=metasearch
  &action=search
  &context=Your+Name+for+the+Search
  &context_url=http%3A%2F%2Flibrary.example.com%2F
  &query=xml&database=CAL01039
}}}

=Holdings links for catalog's with no bibliographic ID in records=

To have Xerxes show a link to holdings for your catalog search results, you can simply enter the link syntax in the `Link to Holdings in Native Interface` field of the IRD in Metalib.  

However, that syntax assumes that you will use a single field from the MARC record (usually one containing the bibliographic ID number of the record) to construct the link.  Some systems, such as Innovative, however, don't expose a bibliographic ID number in their Z39.50 response, so no single linking syntax will work.

In these cases, you have the option of constructing a more complex linking syntax (based on a variety of fields) using the `additional_record_links` template in Xerxes.  Add this to your local demo/xsl/includes.xsl.

Xerxes will call this template for _every_ record, so you need to first test for the Metalib ID of your catalog (in the example below it is `CAL13702`), and then create the link using data from the record.

Here's an example for Innovative systems that works pretty well:

{{{

<xsl:template name="additional_record_links">
	
	<xsl:if test="metalib_id = 'CAL13702'">
		
		<xsl:variable name="catalog_link">
			<xsl:text>http://opac.csuchico.edu/search/</xsl:text>
			<xsl:choose>
				<xsl:when test="standard_numbers/oclc">
					<xsl:text>o</xsl:text>
					<xsl:value-of select="standard_numbers/oclc" />
				</xsl:when>
				<xsl:when test="standard_numbers/isbn">
					<xsl:text>i</xsl:text>
					<xsl:value-of select="standard_numbers/isbn" />
				</xsl:when>
					<xsl:when test="standard_numbers/issn">
					<xsl:text>i</xsl:text>
					<xsl:value-of select="standard_numbers/issn" />
				</xsl:when>
				<xsl:otherwise>
					<xsl:text>t</xsl:text>
					<xsl:value-of select="php:function('urlencode', string(title_normalized))" />
				</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
		
		<div class="recordAction textLink">
			<a href="{$catalog_link}">
				<img src="images/book.gif" width="16" height="16" alt="" border="0" />
				<xsl:text> </xsl:text>
				Check availability in catalog
			</a>
		</div>
	
	</xsl:if>
	
</xsl:template>

}}}

=Show user's full name in login=

Add this to your local demo/xsl/includes.xsl. (Note, not all authentication mechanism return the users full name for Xerxes to store and display.)

{{{
<xsl:variable name="text_header_logout">
        <xsl:text>Log-out </xsl:text>
        <xsl:choose>
                <xsl:when test="//request/authorization_info/affiliated[@user_account = 'true']">
                        <xsl:value-of select="//request/session/user_properties[@key = 'first_name']" />
                        <xsl:text> </xsl:text>
                        <xsl:value-of select="//request/session/user_properties[@key = 'last_name']" />
                        <xsl:text> [</xsl:text>
                        <xsl:value-of select="//request/session/username" />    
                        <xsl:text>]</xsl:text>
                </xsl:when>
                <xsl:when test="//session/role = 'guest'">
                        <xsl:text>Guest</xsl:text>
                </xsl:when>
        </xsl:choose>
</xsl:variable>
}}}

=Include a different favicon=

Add this to your local demo/xsl/includes.xsl.  This is useful if you are hosting multiple Xerxes instances on the same server, as in a consortium, and you want each campus instance to use its own favicon.

{{{
<xsl:variable name="text_extra_html_head_content">
	<link rel="shortcut icon" href="http://www.example.com/favicon.ico" />
	<link rel="icon" href="http://www.example.com/favicon.ico" type="image/x-icon" />
</xsl:variable>
}}}

=List the names of the databases in Quick Search=

Add this to your local demo/xsl/includes.xsl.

{{{
<xsl:variable name="text_databases_category_quick_desc">
	<xsl:text>Search </xsl:text>
	<xsl:call-template name="text_number_to_words">
		<xsl:with-param name="number"><xsl:value-of select="count(/*/category/subcategory[1]/database)" /></xsl:with-param>
	</xsl:call-template>
	<xsl:text> of our most popular databases: </xsl:text>
	<xsl:for-each select="/*/category/subcategory[1]/database">
		<xsl:choose>
			<xsl:when test="position() = 1"></xsl:when>
			<xsl:when test="following-sibling::database">
				<xsl:text>, </xsl:text>
			</xsl:when>
			<xsl:otherwise>
				<xsl:text> and </xsl:text>
			</xsl:otherwise>
		</xsl:choose>
		<xsl:value-of select="title_display" />
	</xsl:for-each>
	<xsl:text>.</xsl:text>
</xsl:variable>
}}}
